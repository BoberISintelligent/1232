Properties liquibaseProperties = new Properties()
liquibaseProperties.load(new FileInputStream(
        file("db/database.properties").exists() ? file("db/database.properties") : file("db/database.properties.sample")))


buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'
        liquibaseVersion = '2.0.1'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.liquibase:liquibase-gradle-plugin:${liquibaseVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.liquibase.gradle'


group = 'ee.urgas'
version = '1'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    // Logging
    implementation 'io.github.microutils:kotlin-logging:1.7.6'

    // Database
    implementation 'org.jetbrains.exposed:exposed:0.17.5'
    implementation 'com.zaxxer:HikariCP:3.4.1'
    implementation "org.postgresql:postgresql:42.2.8"
    liquibaseRuntime 'org.liquibase:liquibase-core:3.6.1'
    liquibaseRuntime 'org.postgresql:postgresql:42.2.8'

    // Kotlin support
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}


liquibase {
    activities {
        main {
            changeLogFile 'db/changelog.xml'
            url liquibaseProperties.url
            username liquibaseProperties.username
            password liquibaseProperties.password
        }
    }
}
